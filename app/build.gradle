apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion rootProject.compileSdk

    defaultConfig {
        applicationId "com.mreigar.transportapp"
        minSdkVersion rootProject.minSdk
        targetSdkVersion rootProject.targetSdk
        versionCode rootProject.versionCode
        versionName rootProject.versionName

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    signingConfigs {
        release {
            // Load keystore
            Properties properties = new Properties()
            if (System.getenv('KEY_PATH') != null) {
                properties.setProperty('STORE_FILE', System.getenv('KEY_PATH'))
                properties.setProperty('STORE_PASS', System.getenv('KEY_STORE_PASSWORD'))
                properties.setProperty('KEY_ALIAS', System.getenv('ALIAS'))
                properties.setProperty('KEY_PASS', System.getenv('KEY_PASSWORD'))
            } else {
                properties.load(new FileInputStream("$projectDir/keystores/prodtest.properties"))
            }

            storeFile file(properties['STORE_FILE'])
            storePassword properties['STORE_PASS']
            keyAlias properties['KEY_ALIAS']
            keyPassword properties['KEY_PASS']
        }
    }

    buildTypes {
        debug {
            debuggable true
            testCoverageEnabled true
        }
        release {
            debuggable false
            minifyEnabled true
            shrinkResources true
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation project(':presentation')
    api project(':network')

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    implementation "org.koin:koin-android:$koin_version"
    testImplementation "org.koin:koin-test:$koin_version"

    implementation 'androidx.core:core-ktx:1.3.1'
    implementation 'androidx.appcompat:appcompat:1.2.0'

    implementation 'com.google.android.gms:play-services-maps:17.0.0'
    implementation 'com.google.maps.android:android-maps-utils:0.5'

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    testImplementation "org.assertj:assertj-core:$assertj_version"
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'

}